import com.stepsoln.core.util.DateUtil;
import com.stepsoln.core.rules.Question;
import com.stepsoln.core.rules.requirements.*;
import java.lang.Boolean;
import org.apache.commons.lang.StringUtils;

global com.stepsoln.core.rules.RulesResponse rulesResponse;
global org.slf4j.Logger logger;

/**
 * Following form are not to be electronically signed:
 * 'APPLICANTAUT'
 * 'DLVR2EX2'
 * 'DLVR1EX1'
 * 'DLVR1EX2'
 */
rule "APPPART2"
salience 100
	when
		CaseData(caseSource in ('CAPTIVE', 'EXTERNAL'));
		form:RequirementForm(code=='APPPART2');
    then
    	rulesResponse.addSignatureRequirement(form.getFormId(), "PRIMARY_INSURED");
    	
end

rule "FULLHIPAAPDF"
salience 100
	when
		CaseData(caseSource in ('CAPTIVE', 'EXTERNAL','INTERNET'));
		form:RequirementForm(code=='FULLHIPAAPDF');
		RequirementForm(code=='APPPART2', $sharedSigFormId:formId);
    then
    	rulesResponse.addSignatureRequirement(form.getFormId(), "PRIMARY_INSURED", $sharedSigFormId);
    	
end

rule "FCRA required if case created on or after July 31, 2013"
salience 90
	when
		CaseData(caseSource in ('CAPTIVE', 'EXTERNAL','INTERNET'), createdDate >= DateUtil.parseDate(DateUtil.FORMAT_YYYYMMDD, "20130731"));
		form:RequirementForm(code in ('FCRA'));
		RequirementForm(code=='APPPART2', $sharedSigFormId:formId);
    then
    	rulesResponse.addSignatureRequirement(form.getFormId(), "PRIMARY_INSURED", $sharedSigFormId);
end

rule "APPPART4"
salience 90
	when
		CaseData(caseSource in ('CAPTIVE', 'EXTERNAL'));
		form:RequirementForm(code in ('APPPART4'));
		RequirementForm(code=='APPPART2', $sharedSigFormId:formId);
    then
    	rulesResponse.addSignatureRequirement(form.getFormId(), "PRIMARY_INSURED", $sharedSigFormId);
end

rule "APPPART2 POLICY_OWNER"
salience 100
	when
		CaseData(caseSource in ('CAPTIVE', 'EXTERNAL','INTERNET'));
		$form:RequirementForm(code=='APPPART2');
		Question(id=="oth_owner_ind", answerAsBoolean==true);
    then
    	rulesResponse.addSignatureRequirement($form.getFormId(), "POLICY_OWNER");
end

rule "APPPART4 POLICY_OWNER"
	when
		CaseData(caseSource in ('CAPTIVE', 'EXTERNAL'), locked==true);
		form:RequirementForm(code=='APPPART4');
		Question(id=="oth_owner_ind", answerAsBoolean==true);
    then
    	rulesResponse.addSignatureRequirement(form.getFormId(), "POLICY_OWNER", true);
end

rule "APPPART4 POLICY_OWNER for paper case"
	when
		CaseData(caseSource in ('CAPTIVE', 'EXTERNAL'), paperCase==true);
		form:RequirementForm(code=='APPPART4');
		Question(id=="oth_owner_ind", answerAsBoolean==true);
    then
    	rulesResponse.addSignatureRequirement(form.getFormId(), "POLICY_OWNER");
end

rule "OWNEREFORM"
	when
		CaseData(caseSource in ('CAPTIVE', 'EXTERNAL','INTERNET'));
		form:RequirementForm(code=='OWNEREFORM');
		Question(id=="oth_owner_ind", answerAsBoolean==true);
    then
    	rulesResponse.addSignatureRequirement(form.getFormId(), "POLICY_OWNER");
    	logger.debug("Rule: " + form.getCode() + " sig required for POLICY_OWNER" );
end


rule "REDOSIGOWNER"
	when
		CaseData(caseSource in ('CAPTIVE', 'EXTERNAL'));
		form:RequirementForm(code=='REDOSIGOWNER');
    then
    	rulesResponse.addSignatureRequirement(form.getFormId(), "PRIMARY_INSURED");
end

rule "REDOSIGINSURE"
	when
		CaseData(caseSource in ('CAPTIVE', 'EXTERNAL'));
		form:RequirementForm(code=='REDOSIGINSURE');
    then
    	rulesResponse.addSignatureRequirement(form.getFormId(), "PRIMARY_INSURED");
end




rule "Remove FCRA if case created before July 31, 2013"
salience 90
	when
		CaseData(caseSource in ('CAPTIVE', 'EXTERNAL','INTERNET'), createdDate < DateUtil.parseDate(DateUtil.FORMAT_YYYYMMDD, "20130731"));
		form:RequirementForm(code in ('FCRA'));
		RequirementForm(code=='FULLHIPAAPDF', $sharedSigFormId:formId);
    then
    	rulesResponse.getSignatureRequirements().remove(form.getFormId());
end

rule "APPPART5"
	when
		CaseData(caseSource in ('CAPTIVE', 'EXTERNAL'), locked==true, StringUtils.isBlank(svcOu)==true, paperCase==false);
		form:RequirementForm(code=='APPPART5');
		AgencyData(agencyCaptive==false);
    then
    	rulesResponse.addSignatureRequirement(form.getFormId(), "AGENT", true);
    	logger.debug("Rule: " + form.getCode() + " sig required for AGENT");
end

rule "APPPART5 for paper case"
	when
		CaseData(caseSource in ('CAPTIVE', 'EXTERNAL'), paperCase==true, agentSignatureExists==true);
		form:RequirementForm(code=='APPPART5');
		Question(id=="agent_id");
		Question(id=="printed_name");
		AgencyData(agencyCaptive==false);
    then
    	rulesResponse.addSignatureRequirement(form.getFormId(), "AGENT");
    	logger.debug("Rule: " + form.getCode() + " sig required for AGENT");
end

rule "Applicant Signature"
	when
		form:RequirementForm(code=='ASRRF');
    then
    	rulesResponse.addSignatureRequirement(form.getFormId(), "PRIMARY_INSURED"); 	
end
